plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
}

group = 'dev.linkedlogics'
version = '1.0.0'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacoco {
    reportsDirectory = file("$buildDir/reports/jacoco")
}

dependencies {
	implementation 'dev.linkedlogics:linkedlogics:1.0.0'
	compileOnly 'org.springframework.boot:spring-boot-starter:2.5.7'
	compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'dev.linkedlogics'
            artifactId = 'linkedlogics-spring'
            version = "$version"

            from components.java
        
            pom {
                name = 'LinkedLogics Spring'
                description = 'Linked-Logics Framework'
                url = 'http://www.linkedlogics.dev'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'rdavudov'
                        name = 'Rajab Davudov'
                        email = 'radjab@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://linkedlogics/linkedlogics-spring.git'
                    developerConnection = 'scm:git:ssh://linkedlogics/linkedlogics-spring.git'
                    url = 'http://www.linkedlogics.dev'
                }
            }
        }
    }
}


